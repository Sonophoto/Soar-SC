sp {apply*build-building
   (state <s> ^operator <op>
              ^io.input-link <i>
              ^io.output-link <o>
              ^buildings <b>)
   (<op> ^name build-building
         ^worker <id>
         ^location-x <x>
         ^location-y <y>
         ^type <type-id>)
   (<i> ^types.type <t>)
   (<b> ^building <building>)
   (<building> ^type <type-id>
               ^count <c>)
-->
   (<o> ^build <b>)
   (<b> ^type <type-id>
        ^location-x <x>
        ^location-y <y>
        ^worker <id>)
   (<building> ^count <c> -
               ^count (+ <c> 1))
}

#Preference rules
sp {pref*buildings-better-than-units
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name build-unit)
   (<o2> ^name build-building)
-->
   (<s> ^operator <o2> > <o2>)
}

sp {pref*idle-better-than-non-idle
   (state <s> ^operator <o1> +
              ^operator <o2> +
              ^io.input-link.units <units>)
   (<o1> ^name build-building
         ^worker <id1>)
   (<o2> ^name build-building
         ^worker <id2>)
   (<units> ^unit <u1>
            ^unit <u2>)
   (<u1> ^id <id1>
         ^idle 1)
   (<u2> ^id <id2>
         ^idle 0)
-->
   (<s> ^operator <o1> > <o2>)
}

#Preference rules

sp {pref*non-carrying-better-than-carrying
   (state <s> ^operator <o1> +
              ^operator <o2> +
              ^io.input-link.units <units>)
   (<o1> ^name build-building
         ^worker <id1>)
   (<o2> ^name build-building
         ^worker <id2>)
   (<units> ^unit <u1>
            ^unit <u2>)
   (<u1> ^id <id1>
         ^carrying 0)
   (<u2> ^id <id2>
         ^carrying 1)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {pref*remove-build-building-operator-if-already-building
   (state <s> ^operator <op> +
              ^io.input-link <i>)
   (<i> ^units.unit <u>
        ^types.type <t>)
   (<t> ^name |Terran Supply Depot|
        ^id <type-id>)
   (<op> ^name build-building
         ^type <type-id>)
   (<u> ^constructing <type-id>)
-->
   (<s> ^operator <op> -)
}

sp {pref*remove-build-supply-depot-operator-if-not-yet-building-but-will
   (state <s> ^operator <op> +
              ^io.output-link.build <b>
              ^io.input-link.types.type <t>)
   (<t> ^name |Terran Supply Depot|
        ^id <type-id>)
   (<b> ^type <type-id>
       -^status)
   (<op> ^name build-building
         ^type <type-id>)
-->
   (<s> ^operator <op> -)
}
