sp {propose*build-barracks
   (state <s> ^name Soar-SC
              ^superstate nil
              ^io.input-link <i>)
   (<i> ^minerals <m> > 149
        ^units.unit <u>
        ^types.type <t>
        ^barracks { <l> < 2 })
   (<t> ^name |Terran SCV|
        ^id <type-id>)
   (<u> ^type <type-id>
        ^idle <idle>
        ^carrying <carrying>
        ^id <id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name build-building
        ^worker <id>
        ^idle <idle>
        ^carrying <carrying>
        ^location <l>
        ^type <type-id>)
}

sp {elaboration*remove-build-barracks-operator-if-already-building
   (state <s> ^operator <op> +
              ^io.input-link <i>)
   (<i> ^units.unit <u>
        ^types.type <t>)
   (<t> ^name |Terran Barracks|
        ^id <type-id>)
   (<op> ^name build-building
         ^type <type-id>)
   (<u> ^constructing <type-id>)
-->
   (<s> ^operator <op> -)
}

sp {elaboration*remove-build-barracks-operator-if-not-yet-building-but-will
   (state <s> ^operator <op> +
              ^io.output-link.build <b>
              ^io.input-link.types.type <t>)
   (<t> ^name |Terran Barracks|
        ^id <type-id>)
   (<b> ^type <type-id>
       -^status)
   (<op> ^name build-building
         ^type <type-id>)
-->
   (<s> ^operator <op> -)
}

sp {elaboration*building-barracks-better-than-gather-minerals
   (state <s> ^operator <o1> +
              ^operator <o2> +
              ^io.input-link.types.type <t>)
   (<t> ^name |Terran Barracks|
        ^type <type-id>)
   (<o1> ^name build-building
         ^type <type-id>)
   (<o2> ^name gather-minerals)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {elaboration*barracks*location-to-x-y-0
   (state <s> ^operator <o>)
   (<o> ^name build-barracks
        ^location 0)
-->
   (<o> ^location-x 40
        ^location-y 60)
}

sp {elaboration*barracks*location-to-x-y-1
   (state <s> ^operator <o>)
   (<o> ^name build-barracks
        ^location 1)
-->
   (<o> ^location-x 46
        ^location-y 60)
}

sp {apply*build-barracks
   (state <s> ^operator <op>
              ^io.input-link <i>
              ^io.output-link <o>)
   (<op> ^name build-barracks
         ^worker <id>
         ^location-x <x>
         ^location-y <y>)
   (<i> ^types.type <t>
        ^barracks <number>)
   (<t> ^name |Terran Barracks|
        ^id <type-id>)
-->
   (<o> ^build <b>)
   (<b> ^type <type-id>
        ^location-x <x>
        ^location-y <y>
        ^worker <id>)
   (<i> ^barracks <number> -
        ^barracks (+ <number> 1))
}