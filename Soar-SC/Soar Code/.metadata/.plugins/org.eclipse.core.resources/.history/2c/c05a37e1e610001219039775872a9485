sp {propose*new-scout
   (state <s> ^name Soar-SC
              ^io.input-link <input-link>)
   (<input-link> ^fog-tiles <fog-tiles>
                 ^units.unit <scv>
                 ^types.type <scv-type>)
   (<scv-type> ^name |Terran SCV|
               ^id <scv-type-id>)
   (<scv> ^type <scv-type-id>
         -^scouting
          ^idle 1
          ^id <scv-id>)
   -{(<input-link> ^unit.unit <other-scv>)
     (<other-scv> ^scouting)}
-->
   (<s> ^operator <scout-operator> + >)
   (<scout-operator> ^name scout
                     ^id <scv-id>)
}

sp {pref*all-scouts-are-equal
   (state <s> ^name Soar-SC
              ^operator <scout1> +
              ^operator <scout2> +)
   (<scout1> ^name scout)
   (<scout2> ^name scout)
-->
   (<s> ^operator <scout1> = <scout2>)
}

sp {apply*new-scout
   (state <s> ^operator <o>
              ^io.input-link.units.unit <scv>)
   (<o> ^name scout
        ^id <scv-id>)
   (<scv> ^id <scv-id>)
-->
   (<scv> ^scouting 1
          ^last-scout-tile |-1 -1 0|)
}

sp {propose*scouting
   (state <s> ^name Soar-SC
              ^io.input-link.units.unit <scv>
              ^io.input-link.fog-tiles <fog-tiles>)
   (<scv> ^scouting 1
          ^idle 1
          ^id <scv-id>)
-->
   (<s> ^operator <start-scouting> + >)
   (<start-scouting> ^name scouting
                     ^id <scv-id>)
}

sp {pref*stop-scouting-if-none-left
   (state <s> ^name Soar-SC
              ^operator <scout-operator> +
              ^io.input-link.fog-tiles <fog-tiles>)
   -{(<fog-tiles> ^<attr> <tile>)
     (<tile> -^timeout)}
	(<scout-operator> ^name scouting)
-->
   (<s> ^operator <scout-operator> -)
}

sp {pref*stop-scouting-if-none-left-that-arent-timed-out
   (state <s> ^name Soar-SC
              ^operator <scout-operator> +
              ^io.input-link.fog-tiles <fog-tiles>)
   -{(<fog-tiles> ^<attr> <tile>)}
	(<scout-operator> ^name scouting)
-->
   (<s> ^operator <scout-operator> -)
}

#substate

sp {propose*initialize-scouting-substate
   (state <s> -^name
               ^superstate <ss>)
   (<ss> ^operator <operator>)
   (<operator> ^name scouting)
-->
   (<s> ^operator <op> + >)
   (<op> ^name init-scouting)
}

sp {apply*initialize-scouting-substate
   (state <s> ^operator.name init-scouting
              ^superstate <ss>)
   (<ss> ^operator <operator>
         ^io.input-link.units.unit <scv>)
   (<operator> ^name scouting
               ^id <scv-id>)
   (<scv> ^id <scv-id>)
-->
   (<s> ^name scouting-substate
        ^selected-unit <scv>)
}

sp {propose*find-closest-fog-tile
   (state <s> ^name scouting-substate
              ^superstate <ss>)
   (<ss> ^svs.command <command>
         ^operator <scouting-operator>)
   (<scouting-operator> ^name scouting
                       -^tile)
   -{(<command> ^extract <extract-command>)
     (<extract-command> ^type closest
                        ^status success)}
-->
   (<s> ^operator <find-closest> + =)
   (<find-closest> ^name find-closest)
}

sp {apply*find-closest-fog-tile
   (state <s> ^name scouting-substate
              ^superstate <ss>
              ^selected-unit <scv-unit>
              ^operator.name find-closest)
   (<ss> ^svs.command <commands>)
   (<scv-unit> ^svsobject <svsobject>)
-->
   (<commands> ^extract <extract-command>)
   (<extract-command> ^type closest ^a <node1> ^b <node2>)
   (<node1> ^type combine
            ^scv <scv>)
   (<node2> ^type combine)
   (<scv> ^type node ^id <svsobject>)
}

sp {apply*find-closest-fog-tile-create-tile
   (state <s> ^name scouting-substate
              ^superstate <ss>
              ^operator.name find-closest)
   (<ss> ^svs.command.extract <extract-command>
         ^io.input-link.fog-tiles <fog-tiles>)
   (<extract-command> ^type closest
                      ^b <node2>)
   (<fog-tiles> ^<attr> <tile>)
   (<tile> ^svsobject <id>
          -^timeout)
-->
   (<node2> ^tile <tile-svs>)
   (<tile-svs> ^type node ^id <id>)
}

sp {propose*check-prior-use-of-scout-tile
   (state <s> ^name scouting-substate
              ^superstate <ss>)
   (<ss> ^svs.command.extract <extract>)
   (<extract> ^type closest
              ^status success
              ^result.record.params.b <tile>)
-->
   (<s> ^operator <check-operator>)
   (<check-operator> ^name scouting-tile-check
                     ^tile <tile>)
}

sp {apply*check-prior-use-of-scout-tile-scout-incorrect
   (state <s> ^superstate <ss>
              ^operator <check-operator>
              ^selected-unit <scv>)
   (<ss> ^operator <scout-operator>
         ^io.input-link.fog-tiles <fog-tiles>
         ^svs.command <command>)
   (<fog-tiles> ^<temp-attr> <fog-tile>)
   (<command> ^extract <extract>)
   (<scv> ^last-scout-tile <tile>)
   (<check-operator> ^tile <tile>)
   (<scout-operator> ^name scouting)
   (<fog-tile> ^svsobject <tile>)
   (<extract> ^type closest
              ^status success)
-->
   (<fog-tile> ^timeout 1)
   (<command> ^extract <extract> -)
}

sp {apply*check-prior-use-of-scout-tile-scout-correct
   (state <s> ^superstate <ss>
              ^operator <check-operator>
              ^selected-unit <scv>)
   (<ss> ^operator <scout-operator>
         ^svs.command <command>)
   (<command> ^extract <extract>)
   (<scv> ^last-scout-tile <tile>)
   (<check-operator> ^tile { <> <tile> <new-tile> })
   (<scout-operator> ^name scouting)
   (<extract> ^type closest
              ^status success)
-->
   (<scout-operator> ^tile <new-tile>)
   (<command> ^extract <extract> -)
}

sp {apply*scouting
   (state <s> ^name Soar-SC
              ^operator <scouting>
              ^io.output-link <output-link>
              ^io.input-link <input-link>)
   (<scouting> ^name scouting
               ^id <scv-id>
               ^tile <fog-tile>)
   (<input-link> ^units.unit <scv>)
   (<scv> ^id <scv-id>
          ^last-scout-tile <old-tile>)
-->
   (<scv> ^last-scout-tile <old-tile> -
          ^last-scout-tile <fog-tile>)
   (<output-link> ^move <command>)
   (<command> ^object <scv-id>
              ^location-string <fog-tile>)
}
