sp {propose*build-barracks
   (state <s> ^name Soar-SC
              ^superstate nil
              ^io.input-link <i>
              ^buildings.building <b>)
   (<i> ^minerals <m> > 149
        ^units.unit <u>
        ^types.type <t>
        ^types.type <t-s>)
   (<t> ^name |Terran SCV|
        ^id <type-id>)
   (<u> ^type <type-id>
        ^idle <idle>
        ^carrying <carrying>
        ^id <id>)
   (<t-s> ^name |Terran Barracks|
          ^id <barracks-id>)
   (<b> ^type <barracks-id>
        ^count { <l> < 2 })
-->
   (<s> ^operator <o> + =)
   (<o> ^name build-building
        ^worker <id>
        ^idle <idle>
        ^carrying <carrying>
        ^location <l>
        ^type <barracks-id>)
   (interrupt)
}

#Elaboration rules

sp {pref*building-barracks-better-than-gather-minerals
   (state <s> ^operator <o1> +
              ^operator <o2> +
              ^io.input-link.types.type <t>)
   (<t> ^name |Terran Barracks|
        ^id <type-id>)
   (<o1> ^name build-building
         ^type <type-id>)
   (<o2> ^name gather-minerals)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {elaboration*barracks*location-to-x-y-0
   (state <s> ^operator <o> +
              ^io.input-link.types.type <t>)
   (<t> ^name |Terran Barracks|
        ^id <type-id>)
   (<o> ^name build-building
        ^type <type-id>
        ^location 0)
-->
   (<o> ^location-x 40
        ^location-y 60)
   (interrupt)
}

sp {elaboration*barracks*location-to-x-y-1
   (state <s> ^operator <o> +
              ^io.input-link.types.type <t>)
   (<t> ^name |Terran Barracks|
        ^id <type-id>)
   (<o> ^name build-building
        ^type <type-id>
        ^location 1)
-->
   (<o> ^location-x 46
        ^location-y 60)
   (interrupt)
}
