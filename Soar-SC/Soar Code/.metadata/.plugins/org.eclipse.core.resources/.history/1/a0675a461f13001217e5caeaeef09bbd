sp {propose*attack-unit
   (state <s> ^name Soar-SC
              ^io.input-link.enemy-units <enemy-units>
              ^io.input-link.units <my-units>
              ^io.input-link.types.type <marine-type>
              ^io.output-link <output-link>)
   (<enemy-units> ^unit <enemy>)
   (<enemy> ^id <enemy-id>)
   (<marine-type> ^name |Terran Marine|
                  ^id <type-id>)
   (<my-units> ^unit <my-unit>)
   (<my-unit> ^type <type-id>
              ^id <marine-id>
             -^unit-attack)
-->
   (<s> ^operator <attack-operator> + >)
   (<attack-operator> ^name attack-unit
                      ^enemy <enemy-id>
                      ^marine <marine-id>)
}

sp {propose*attack-building
   (state <s> ^name Soar-SC
              ^io.input-link.enemy-units <enemy-units>
              ^io.input-link.units <my-units>
              ^io.input-link.types.type <marine-type>
              ^io.output-link <output-link>)
   (<enemy-units> ^building <enemy>)
   (<enemy> ^id <enemy-id>)
   (<marine-type> ^name |Terran Marine|
                  ^id <type-id>)
   (<my-units> ^unit <my-unit>)
   (<my-unit> ^type <type-id>
              ^id <marine-id>
             -^building-attack
             -^unit-attack)
-->
   (<s> ^operator <attack-operator> + >)
   (<attack-operator> ^name attack-building
                      ^enemy <enemy-id>
                      ^marine <marine-id>)
}

sp {pref*all-unit-attacks-are-equal
   (state <s> ^name Soar-SC
              ^operator <unit-attack1> +
              ^operator <unit-attack2> +)
   (<unit-attack1> ^name attack-unit)
   (<unit-attack2> ^name attack-unit)
-->
   (<s> ^operator <unit-attack1> = <unit-attack2>)
}

sp {pref*all-building-attacks-are-equal
   (state <s> ^name Soar-SC
              ^operator <building-attack1> +
              ^operator <building-attack2> +)
   (<building-attack1> ^name attack-building)
   (<building-attack2> ^name attack-building)
-->
   (<s> ^operator <building-attack1> = <building-attack2>)
}

sp {pref*unit-attack-better-than-all
   (state <s> ^name Soar-SC
              ^operator <unit-attack> +
              ^operator <other> +)
   (<unit-attack> ^name attack-unit)
   (<other> ^name { <> attack-unit })
-->
   (<s> ^operator <unit-attack> > <other>)
}

sp {pref*building-attack-better-than-everything-but-unit
   (state <s> ^name Soar-SC
              ^operator <building-attack> +
              ^operator <other> +)
   (<building-attack> ^name attack-building)
   (<other> ^name { <> attack-unit }
            ^name { <> attack-building })
-->
   (<s> ^operator <building-attack> > <other>)
}

sp {apply*attack-unit
   (state <s> ^name Soar-SC
              ^operator <attack-operator>
              ^io.output-link <output-link>
              ^io.input-link.units.unit <marine>)
   (<attack-operator> ^name attack-unit
                      ^enemy <enemy-id>
                      ^marine <marine-id>)
   (<marine> ^id <marine-id>)
-->
   (<output-link> ^move <attack>)
   (<attack> ^object <marine-id>
             ^dest <enemy-id>)
   (<marine> ^unit-attack 1)
}

sp {apply*attack-building
   (state <s> ^name Soar-SC
              ^operator <attack-operator>
              ^io.output-link <output-link>
              ^io.input-link.units.unit <marine>)
   (<attack-operator> ^name attack-building
                      ^enemy <enemy-id>
                      ^marine <marine-id>)
   (<marine> ^id <marine-id>)
-->
   (<output-link> ^move <attack>)
   (<attack> ^object <marine-id>
             ^dest <enemy-id>)
   (<marine> ^building-attack 1)
}

sp {remove*unit-attack-if-attacking-building
   (state <s> ^name Soar-SC
              ^operator <o>
              ^io.input-link.units.unit <marine>
              ^io.input-link.types.type <marine-type>
              ^io.input-link.types.type <target-type>
              ^io.input-link.units <units>)
   (<units> ^<attr> <target>)
   (<marine> ^type <marine-type-id>
             ^target <targetID>
             ^unit-attack <unit-attack>)
   (<marine-type> ^name |Terran Marine|
                  ^id <marine-type-id>)
   (<target> ^id <targetID>
             ^type <target-type-id>)
   (<target-type> ^id <target-type-id>
                  ^building 1)
-->
   (<marine> ^unit-attack <unit-attack> -)
}

sp {remove*building-attack-if-attacking-unit
   (state <s> ^name Soar-SC
              ^operator <o>
              ^io.input-link.units.unit <marine>
              ^io.input-link.types.type <marine-type>
              ^io.input-link.types.type <target-type>
              ^io.input-link.units <units>)
   (<units> ^<attr> <target>)
   (<marine> ^type <marine-type-id>
             ^target <targetID>
             ^building-attack <building-attack>)
   (<marine-type> ^name |Terran Marine|
                  ^id <marine-type-id>)
   (<target> ^id <targetID>
             ^type <target-type-id>)
   (<target-type> ^id <target-type-id>
                  ^building 0)
-->
   (<marine> ^building-attack <building-attack> -)
}

sp {remove*unit-attack-if-no-target
   (state <s> ^name Soar-SC
              ^operator <o>
              ^io.input-link.units.unit <marine>
              ^io.input-link.types.type <marine-type>)
   (<marine> ^type <marine-type-id>
             ^target 0
             ^unit-attack <unit-attack>)
   (<marine-type> ^name |Terran Marine|
                  ^id <marine-type-id>)
-->
   (<marine> ^unit-attack <unit-attack> -)
}

sp {remove*building-attack-if-no-target
   (state <s> ^name Soar-SC
              ^operator <o>
              ^io.input-link.units.unit <marine>
              ^io.input-link.types.type <marine-type>)
   (<marine> ^type <marine-type-id>
             ^target 0
             ^building-attack <building-attack>)
   (<marine-type> ^name |Terran Marine|
                  ^id <marine-type-id>)
-->
   (<marine> ^building-attack <building-attack> -)
}
