sp {propose*attack
   (state <s> ^name Soar-SC
              ^io.input-link.enemy-units <enemy-units>
              ^io.input-link.units <my-units>
              ^io.input-link.types.type <marine-type>
              ^io.output-link <output-link>)
   (<enemy-units> ^<attr> <enemy>)
   (<enemy> ^id <enemy-id>)
   (<marine-type> ^name |Terran Marine|
                  ^id <type-id>)
   (<my-units> ^unit <my-unit>)
   (<my-unit> ^type <type-id>
              ^id <marine-id>
              ^idle 1)
-->
   (<s> ^operator <attack-operator> + >)
   (<attack-operator> ^name attack
                      ^enemy <enemy-id>
                      ^marine <marine-id>)
}

sp {pref*all-attacks-are-equal
   (state <s> ^name Soar-SC
              ^operator <attack1> +
              ^operator <attack2> +)
   (<attack1> ^name attack
             -^counter-attack)
   (<attack2> ^name attack
             -^counter-attack)
-->
   (<s> ^operator <attack1> = <attack2>)
}

sp {pref*attack-better-than-all
   (state <s> ^name Soar-SC
              ^operator <non-attack> +
              ^operator <attack> +)
   (<non-attack> ^name { <> attack })
   (<attack> ^name attack)
-->
   (<s> ^operator <attack> > <non-attack>)
}

sp {apply*attack
   (state <s> ^name Soar-SC
              ^operator <attack-operator>
              ^io.output-link <output-link>)
   (<attack-operator> ^name attack
                      ^enemy <enemy-id>
                      ^marine <marine-id>)
-->
   (<output-link> ^move <attack>)
   (<attack> ^object <marine-id>
             ^dest <enemy-id>)
   (write |Attacking | <enemy-id> | with | <marine-id> | )
}

sp {propose*counter-attack
   (state <s> ^name Soar-SC
              ^io.input-link <input-link>)
   (<input-link> ^units.unit <marine>
                 ^enemy-units.unit <enemy>
                 ^types.type <marine-type>)
   (<enemy> ^id <enemy-id>
            ^type <marine-type-id>
            ^target <marine-id>)
   (<marine> ^id <marine-id>
             ^type <marine-type-id>
             ^target { <> <enemy-id> })
   (<marine-type> ^name |Terran Marine|
                  ^id <marine-type-id>)
-->
   (<s> ^operator <counter-attack>)
   (<counter-attack> ^name attack
                     ^counter-attack 1
                     ^enemy <enemy-id>
                     ^marine <marine-id>)
}

sp {pref*remove-new-counter-attack-if-already-counter-attacking
   (state <s> ^name Soar-SC
              ^operator <ca> +
              ^io.input-link <input-link>)
   (<ca> ^name attack
         ^counter-attack 1
         ^marine <marine-id>)
   (<input-link> ^units.unit <marine>
                 ^enemy-units.unit <enemy-marine>
                 ^types.type <marine-type>)
   (<marine-type> ^name |Terran Marine|
                  ^id <marine-type-id>)
   (<marine> ^id <marine-id>
             ^target <target-id>)
   (<enemy-marine> ^id <target-id>
                   ^target <marine-id>)
-->
   (<s> ^operator <ca> -)
}  

sp {pref*all-counter-attacks-are-equal
   (state <s> ^name Soar-SC
              ^operator <ca1> +
              ^operator <ca2> +)
   (<ca1> ^name attack
          ^counter-attack)
   (<ca2> ^name attack
          ^counter-attack)
-->
   (<s> ^operator <ca1> = <ca2>)
}

sp {pref*counter-attacks-better-than-regular-attacks
   (state <s> ^name Soar-SC
              ^operator <ca> +
              ^operator <a> +)
   (<ca> ^name attack
         ^counter-attack)
   (<a> ^name attack
       -^counter-attack)
-->
   (<s> ^operator <ca> > <a>)
}

sp {apply*secondary
   (state <s> ^name Soar-SC
              ^operator <ca>)
   (<ca> ^name attack
         ^counter-attack)
-->
   (write |Counter Attack!| (crlf))
}
