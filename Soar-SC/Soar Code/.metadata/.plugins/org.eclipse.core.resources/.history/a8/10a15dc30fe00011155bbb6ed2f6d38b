sp {propose*build-scv
   (state <s> ^name Soar-SC
              ^io.input-link <i>)
   (<i> ^minerals <m> > 49
        ^total-supplies <ts>
        ^used-supplies <us> < <ts>
        ^units.building <b>
        ^types.type <scv-type>
        ^types.type <command-center>)
   (<command-center> ^name |Terran Command Center|
                     ^id <command-center-id>)
   (<scv-type> ^name |Terran SCV|
               ^id <type-id>)
   (<b> ^type <command-center-id>
        ^id <id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name build-scv
        ^location <id>
        ^type <type-id>)
}

sp {elaboration*build-scv-better-than-gather-minerals
   (state <s> ^operator <o1>
              ^operator <o2>)
   (<o1> ^name build-scv)
   (<o2> ^name gather-minerals)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {apply*build-scv
   (state <s> ^operator <op>
              ^io.output-link <o>
              ^cycle-count <c>)
   (<op> ^name build-scv
         ^location <id>
         ^type <type-id>)
-->
   (<o> ^build <b>)
   (<b> ^type <type-id>
        ^location <id>
        ^cycle <c>)	
}