sp {propose*gather-minerals
   (state <s> ^name Soar-SC
              ^superstate nil
              ^io.input-link <i>)
   (<i> ^resources.mineral <r>
        ^units.unit <u>)
   (<r> ^id <id>)
   (<u> ^worker 1
        ^idle 1
        ^id <id-o>)
-->
   (<s> ^operator <o> +)
   (<o> ^name gather-minerals
        ^id-dest <id>
        ^id-object <id-o>)
}

sp {apply*gather-minerals
   (state <s> ^name Soar-SC
              ^operator <op>
              ^io.output-link <o>)
   (<op> ^name gather-minerals
         ^id-dest <id>
         ^id-object <id-o>)
-->
   (<o> ^move <m>)
   (<m> ^object <id-o>
        ^dest <id>)
}

sp {propose*query-svs-scv
   (state <s> ^impasse tie
              ^superstate <ss>)
   (<ss> ^name Soar-SC)
-->
   (<s> ^operator <op> + =)
   (<op> ^name query-svs)
}

sp {apply*query-svs-scv-create-objects
   (state <s> ^superstate <ss>
              ^operator.name query-svs)
   (<ss> ^svs.command <c>)
-->
   (<c> ^extract <cmd>)
   (<cmd> ^type closest ^a <node1> ^b <node2>)
   (<node1> ^type combine)
   (<node2> ^type combine)
}

sp {apply*query-svs-scv-create-units
   (state <s> ^superstate <ss>
              ^operator.name query-svs)
   (<ss> ^svs.command.extract <c>
         ^io.input-link <i>)
   (<c> ^a <node1>)
   (<i> ^units.unit <u>)
   (<u> ^svsobject <id-unit>
        ^idle 1
        ^worker 1)
-->
   (<node1> ^<id-unit> <unit>)
   (<unit> ^type node ^id <id-unit>)
   (write |apply*query-svs-scv-create-units executed!| (crlf) )
}

sp {apply*query-svs-scv-create-minerals
   (state <s> ^superstate <ss>
              ^operator.name query-svs)
   (<ss> ^svs.command.extract <c>
         ^io.input-link <i>)
   (<c> ^b <node1>)
   (<i> ^resources.mineral <m>)
   (<m> ^svsobject <id-mineral>)
-->
   (<node1> ^<id-mineral> <mineral>)
   (<mineral> ^type node ^id <id-mineral>)
}

sp {act-on*result-of-query-to-svs-scv
   (state <s> ^impasse tie
              ^superstate <ss>)
   (<ss> ^svs.command.extract <e>
         ^operator <o> +)
   (<e> ^status success
        ^result.record.params <p>)
   (<p> ^a <object>
        ^b <destination>)
   (<ss> ^io.input-link.resources.mineral <mm>
         ^io.input-link.units.unit <uu>)
   (<mm> ^svsobject <destination> ^id <mineral-id>)
   (<uu> ^svsobject <object> ^id <unit-id>)
   (<o> ^id-object <unit-id>
        ^id-dest <mineral-id>)
-->
   (<ss> ^operator <o> >)
}

sp {apply*best-svs-operator
   (state <s> ^superstate nil
              ^operator.name gather-minerals
              ^svs.command <c>)
   (<c> ^extract <e>)
-->
   (<c> ^extract <e> -)
}
   
 