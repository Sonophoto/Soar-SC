sp {propose*build-barracks
   (state <s> ^name Soar-SC
              ^superstate nil
              ^io.input-link <i>)
   (<i> ^minerals <m> > 99
        ^units.unit <u>
        ^types.type <t>
        ^barracks { <l> < 2 })
   (<t> ^name |Terran Barracks|
        ^id <type-id>)
   (<u> ^type <type-id>
        ^idle <idle>
        ^carrying <carrying>
        ^id <id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name build-barracks
        ^worker <id>
        ^idle <idle>
        ^carrying <carrying>
        ^location <l>)
}

sp {elaboration*remove-build-barracks-operator-if-already-building
   (state <s> ^operator <op> +
              ^io.input-link <i>)
   (<i> ^units.unit <u>
        ^types.type <t>)
   (<op> ^name build-supply-depot)
   (<t> ^name |Terran Supply Depot|
        ^id <type-id>)
   (<u> ^constructing 1)
-->
   (<s> ^operator <op> -)
}

sp {elaboration*building-barracks-better-than-build-scv
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name build-barracks)
   (<o2> ^name build-scv)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {elaboration*building-barracks-better-than-gather-minerals
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name build-barracks)
   (<o2> ^name gather-minerals)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {elaboration*building-barracks-idle-better-than-non-idle
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name build-barracks
         ^idle 1)
   (<o2> ^name build-barracks
         ^idle 0)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {elaboration*building-barracks-non-carrying-better-than-carrying
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name build-barracks
         ^carrying 0)
   (<o2> ^name build-barracks
         ^carrying 1)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {elaboration*location-to-x-y-0
   (state <s> ^operator <o>)
   (<o> ^name build-barracks
        ^location 0)
-->
   (<o> ^location-x 42
        ^location-y 47)
}

sp {apply*build-barracks
   (state <s> ^operator <op>
              ^io.input-link <i>
              ^io.output-link <o>)
   (<op> ^name build-barracks
         ^worker <id>
         ^location-x <x>
         ^location-y <y>)
   (<i> ^types.type <t>
        ^barracks <number>)
   (<t> ^name |Terran Barracks|
        ^id <type-id>)
-->
   (<o> ^build <b>)
   (<b> ^type <type-id>
        ^location-x <x>
        ^location-y <y>
        ^worker <id>)
   (<i> ^barracks <number> -
        ^barracks (+ <number> 1))
}