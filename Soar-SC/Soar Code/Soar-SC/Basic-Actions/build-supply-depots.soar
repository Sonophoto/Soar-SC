sp {propose*build-supply-depot
   (state <state> ^name Soar-SC
                  ^superstate nil
                  ^io.input-link <input-link>
                  ^io.output-link <output-link>
                  ^buildings.building <supply-depot-count>
                  ^supply-depot-unit-total <sdut>
                  ^minerals-budget <minerals-budget>)
   (<input-link> ^minerals <m> > 99
                 ^units.unit <scv>
                 ^types.type <scv-type>
                 ^types.type <supply-depot-type>
                 ^total-supplies <ts>
                 ^used-supplies { >= <sdut> <= <ts> <us> } )
   (<scv-type> ^name |Terran_SCV|
               ^id <scv-type-id>)
   (<scv> ^type <scv-type-id>
          ^idle <scv-idle>
          ^carrying <scv-carrying>
          ^id <scv-id>
         -^scouting
          ^constructing -1)
   (<supply-depot-type> ^name |Terran_Supply_Depot|
                        ^id <supply-depot-type-id>
                        ^mineral-cost { <= <minerals-budget> })
  -{ (<output-link> ^build-building <command-to-build>)
     (<command-to-build> ^type <supply-depot-type-id>
                        -^status)}
-->
   (<state> ^operator <build-supply-depot-operator> + =)
   (<build-supply-depot-operator> ^name build-building
                                  ^worker <scv-id>
                                  ^idle <scv-idle>
                                  ^carrying <scv-carrying>
#                                  ^location <l>
                                  ^type <supply-depot-type-id>)
}

#Elaboration Rules
sp {elaboration*supply-depot-unit-total
   (state <state> ^io.input-link.total-supplies <ts>)
-->
   (<state> ^supply-depot-unit-total (- <ts> 5))
}

#Preference rules

sp {pref*building-supply-depot-better-than-gather-minerals
   (state <s> ^operator <o1> +
              ^operator <o2> +
              ^io.input-link.types.type <t>)
   (<t> ^name |Terran_Supply_Depot|
        ^id <type-id>)
   (<o1> ^name build-building
         ^type <type-id>)
   (<o2> ^name gather-minerals)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {pref*building-supply-depot-better-than-build-building
   (state <s> ^operator <o1> +
              ^operator <o2> +
              ^io.input-link.types.type <t>)
   (<t> ^name |Terran_Supply_Depot|
        ^id <type-id>)
   (<o1> ^name build-building
         ^type <type-id>)
   (<o2> ^name build-building
         ^type { <> <type-id> })
-->
   (<s> ^operator <o1> > <o2>)
}