#Propose the scouts
#Can use either a marine or an SCV but prefer a marine
sp {propose*new-scout-scv
   (state <s> ^name Soar-SC
              ^io.input-link <input-link>)
   (<input-link> ^units.unit <scv>
                 ^types.type <scv-type>)
   (<scv-type> ^name |Terran_SCV|
               ^id <scv-type-id>)
   (<scv> ^type <scv-type-id>
          ^id <scv-id>)
   -{(<input-link> ^units.unit <scout>)
     (<scout> ^scouting 1)}
-->
   (<s> ^operator <scout-operator> + >)
   (<scout-operator> ^name new-scout-scv
                     ^id <scv-id>
                     ^type scout)
}

sp {propose*new-scout-marine
   (state <s> ^name Soar-SC
              ^io.input-link <input-link>)
   (<input-link> ^units.unit <marine>
                 ^types.type <marine-type>)
   (<marine-type> ^name |Terran_Marine|
                  ^id <marine-type-id>)
   (<marine> ^type <marine-type-id>
             ^id <marine-id>)
   -{(<input-link> ^units.unit <scout>)
     (<scout> ^scouting 1
              ^type <marine-type-id>)}
-->
   (<s> ^operator <scout-operator> + >)
   (<scout-operator> ^name new-scout-marine
                     ^id <marine-id>
                     ^type scout)
}

#preference marine over scv
sp {pref*marine-over-scv-scout
   (state <s> ^operator <scv-scout> +
              ^operator <marine-scout> +)
   (<scv-scout> ^name new-scout-scv)
   (<marine-scout> ^name new-scout-marine)
-->
   (<s> ^operator <marine-scout> > <scv-scout>)
}

sp {pref*all-scouts-of-same-type-are-equal
   (state <s> ^name Soar-SC
              ^operator <scout1> +
              ^operator <scout2> +)
   (<scout1> ^type scout
             ^name <name>)
   (<scout2> ^type scout
             ^name <name>)
-->
   (<s> ^operator <scout1> = <scout2>)
}

#Create the scout
#Don't care if there is a previous scout like an SCV,
#that'll die eventually... but until then it's useful

sp {apply*new-scout
   (state <s> ^operator <o>
              ^io.input-link.units.unit <unit>)
   (<o> ^type scout
        ^id <unit-id>)
   (<unit> ^id <unit-id>)
-->
   (<unit> ^scouting 1)
   (write |Created new scout (| <unit-id> |)| (crlf) )
}

#Actual scouting code

#Fog tile scouting
sp {propose*scout-fog-tile
   (state <s> ^name Soar-SC
              ^io.input-link.units.unit <scout>
              ^io.input-link.fog-tiles <fog-tiles>)
   (<scout> ^scouting 1
            ^idle 1
            ^id <unit-id>)
   -{(<s> ^io)
     (<fog-tiles> -^fog-tile.timeout)}
-->
   (<s> ^operator <scout-fog-tile> + =)
   (<scout-fog-tile> ^name scout-fog-tile
                     ^id <unit-id>
                     ^type scout)
}

#Key location scouting

#Enemy building scouting
sp {propose*scout-key-location*building
   (state <s> ^name Soar-SC
              ^io.input-link.units.unit <scout>
              ^io.input-link.enemy-units.building <building>)
   (<scout> ^scouting 1
            ^idle 1
            ^id <unit-id>)
   (<building> ^svsobject <svsobject>)
   -{(<scout> ^last-scout-location <svsobject>)}
-->
   (<s> ^operator <scout-key-location> + =)
   (<scout-key-location> ^name scout-key-location
                         ^id <unit-id>
                         ^building <building>
                         ^type scout)
}

#Map scouting
sp {propose*scout-key-location*map
   (state <s> ^name Soar-SC
              ^io.input-link.units.unit <scout>
              ^io.input-link.map-corners.corner <corner>)
   (<scout> ^scouting 1
            ^idle 1
            ^id <unit-id>)
    (<corner> ^svsobject <svsobject>)
   -{(<scout> ^last-scout-location <svsobject>)}
-->
   (<s> ^operator <scout-key-location> + =)
   (<scout-key-location> ^name scout-key-location
                         ^id <unit-id>
                         ^corner <corner>
                         ^type scout)
}

#Preference fog tile scouting over key location scouting
sp {pref*fog-tile-over-key-location-scouting
   (state <s> ^name Soar-SC
              ^operator <scout-fog-tile> +
              ^operator <scout-key-location> +)
   (<scout-fog-tile> ^name scout-fog-tile)
   (<scout-key-location> ^name scout-key-location)
-->
   (<s> ^operator <scout-fog-tile> > <scout-key-location>)
}

#Apply scouting
sp {apply*scout-location
   (state <s> ^name Soar-SC
              ^operator <scout>
              ^io.input-link.units.unit <unit>
              ^io.output-link <output-link>)
   (<scout> ^type scout
            ^location <location-string>
            ^id <unit-id>)
   (<unit> ^id <unit-id>
           ^last-scout-location <last-scout-location>)
-->
   (<output-link> ^move <command>) #Move command
   (<command> ^object <unit-id>
              ^svsobject <location-string>
              ^type svs-coordinates #Tell the middleware that this is an svs coordinates request and it needs to retrieve it's position
              ^position center) #Move to the center, defaults to the upper left
   (<unit> ^last-scout-location <last-scout-location> -
           ^last-scout-location <location-string>)
   (write |Scouting using scout | <unit-id> | at | <location-string> (crlf) )
}

#Substate
#Initially the scout operators don't have the building position or the fog tile position
#Find it and put it on the operator

#Initialize the substate
sp {propose*initialize-scouting-substate
   (state <s> -^name
               ^superstate <super>)
   (<super> ^name Soar-SC
            ^operator <scout>)
   (<scout> ^type scout)
-->
   (<s> ^operator <init-scouting-substate> + >)
   (<init-scouting-substate> ^name init-scouting-substate)
}

sp {apply*initialize-scouting-substate
   (state <s> -^name
               ^operator <init-scouting-substate>
               ^superstate <super>)
   (<init-scouting-substate> ^name init-scouting-substate)
   (<super> ^operator <scout>
            ^io.input-link.units.unit <unit>)
   (<scout> ^type scout
            ^id <unit-id>)
   (<unit> ^id <unit-id>
           ^svsobject <svsobject>
           ^last-scout-location <last-location>)
-->
   (<s> ^name scouting-substate
        ^scout <svsobject>
        ^last-location <last-location>)
}

#If we already know the object to go to, as in it's a key location,
#set the location attribute on the state to skip looking for a fog tile
sp {apply*initialize-scouting-substate*location
   (state <s> ^name scouting-substate
              ^operator <init-scouting-substate>
              ^superstate <super>)
   (<init-scouting-substate> ^name init-scouting-substate)
   (<super> ^name Soar-SC
            ^operator <scout-key-location>)
   (<scout-key-location> ^name scout-key-location
                         ^<attr> <location>)
   (<location> ^svsobject <svsobject>)
-->
   (<s> ^location <svsobject>)
}

#Find the fog tile
#Propose to find it
sp {propose*find-closest-fog-tile
   (state <s> ^name scouting-substate
             -^location
             -^svs.command.extract.status)
-->
   (<s> ^operator <find-closest-fog-tile> +)
   (<find-closest-fog-tile> ^name find-closest-fog-tile)
}

#Create the svs command link
sp {apply*find-closest-fog-tile*create-svs-commands
   (state <s> ^operator.name find-closest-fog-tile
              ^svs.command <command-link>
              ^scout <scout-svsobject>)
-->
   (<command-link> ^extract <extract>)
   (<extract> ^type closest
              ^a <scout-node>
              ^b <tile-node>)
   (<scout-node> ^type combine
            ^scout <scout>)
   (<scout> ^type node
            ^id <scout-svsobject>)
   (<tile-node> ^type combine)
}

#Add the fog tiles
sp {apply*find-closest-fog-tile*add-fog-tiles
   (state <s> ^operator.name find-closest-fog-tile
              ^svs.command.extract.b <tile-node>
              ^superstate <super>)
   (<super> ^io.input-link.fog-tiles <fog-tiles>)
   (<fog-tiles> ^<attr> <tile>)
   (<tile> ^svsobject <svsobject>
          -^timeout)
-->
   (<tile-node> ^tile <tile>)
   (<tile> ^type node
           ^id <svsobject>)
}

#Propose to check if it's a valid selection
sp {propose*check-fog-tile-valid
   (state <s> ^name scouting-substate
              ^svs.command.extract <extract>)
   (<extract> ^type closest
              ^status success
              ^result.record.params.b <tile>)
-->
   (<s> ^operator <check-fog-tile-valid> +)
   (<check-fog-tile-valid> ^name check-fog-tile-valid
                           ^tile <tile>)
}

#This fires when it is a valid selection
#This creates a ^location WME on the state
#just like what would happen for a key location
#operator earlier
sp {apply*check-fog-tile-valid*valid
   (state <s> ^operator <check-fog-tile-valid>
              ^last-location <last-location>
              ^svs.command <command-link>)
   (<check-fog-tile-valid> ^name check-fog-tile-valid
                           ^tile { <> <last-location> <tile> })
   (<command-link> ^extract <extract>)
-->
   (<s> ^location <tile>)
   (<command-link> ^extract <extract> -)
}

#This fires when it is not a valid selection
#It sets a timeout on the tile
#And then removes the extract command.  This
#lets the find fog tile fire again
sp {apply*check-fog-tile-valid*invalid
   (state <s> ^operator <check-fog-tile-valid>
              ^superstate <super>
              ^last-location <last-location>
              ^svs.command <command-link>)
   (<check-fog-tile-valid> ^name check-fog-tile-valid
                           ^tile <last-location>)
   (<super> ^io.input-link.fog-tiles <fog-tiles>)
   (<fog-tiles> ^<attr> <fog-tile>)
   (<fog-tile> ^svsobject <last-location>)
   (<command-link> ^extract <extract>)
-->
   (<fog-tile> ^timeout 1)
   (<command-link> ^extract <extract> -)
}

#Propose to find the location's coordinates since
#we have a location now
sp {propose*set-scout-location
   (state <s> ^name scouting-substate
              ^location <svsobject>)
-->
   (<s> ^operator <set-scout-location> +)
   (<set-scout-location> ^name set-scout-location)
}

sp {apply*set-scout-location
   (state <s> ^operator.name find-location-coordinates
              ^superstate <super>
              ^location <location>)
   (<super> ^operator <scout>)
   (<scout> ^type scout)
-->
   (<scout> ^location <location>)
}
